buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.jruby-gradle:jruby-gradle-jar-plugin:1.1.5'
    }
}

apply plugin: 'java'
apply plugin: 'com.github.jruby-gradle.jar'

repositories {
    mavenCentral()
    jcenter()
    rubygems('https://rubygems.org')
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile (
        'com.amazonaws:aws-lambda-java-core:1.1.0',
        'com.amazonaws:aws-lambda-java-log4j:1.0.0',
        'org.embulk:embulk-core:0.7.10',
        'org.embulk:embulk-standards:+',
        fileTree(dir: 'build/gems/gems/embulk-input-redshift-0.9.3/classpath'),
        fileTree(dir: 'build/gems/gems/embulk-output-redshift-0.8.2/classpath')
    )

    gems (
        'rubygems:embulk-input-redshift:0.9.3',
        'rubygems:embulk-output-redshift:0.8.2'
    )
}

// Task for building the zip file for upload
task buildZip(type: Zip) {
    // Using the Zip API from gradle to build a zip file of all the dependencies
    //
    // The path to this zip file can be set in the serverless.yml file for the
    // package/artifact setting for deployment to the S3 bucket
    //
    // Link: https://docs.gradle.org/current/dsl/org.gradle.api.tasks.bundling.Zip.html

    // set the base name of the zip file
    baseName = "etl"
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtime
    }
}

build.dependsOn buildZip
